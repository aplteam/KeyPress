 r←OnKeyPress(ref event input char key shiftState);kl;H;None;ShiftS;info;buff
 r←0
 None←{''≡⍵:'[none]' ⋄ ⍵}
 H←⎕D,6⍴⎕A                          ⍝ Hex digits
 :If ('EP'≡input)∧shiftState=0
     :If 'EP' 0≡n.∆LastKey          ⍝ <Escape> ?!
         :Trap 0 ⋄ ref.Close ⋄ :EndTrap
         :Return
     :Else
         n.∆LastKey←'EP' 0
     :EndIf
 :ElseIf (115≡key)∧shiftState=4     ⍝ <Alt+F4> ?!
     :If 'ALT+F4' 4≡n.∆LastKey
         ref.Close
         :Return
     :Else
         n.∆LastKey←'ALT+F4' 4
     :EndIf
 :Else
     n.∆LastKey←''
 :EndIf

 n.Object.Text←'n/a'
 n.Event.Text←event
 n.InputCode.Text←None input
 n.CharCode.Text←⍕char
 n.Decimal.Text←H[1+((4)⍴16)⊤char]
 n.KeyNumber.Text←⍕key
 n.ShiftState.Text←None⍕shiftState
 n.ShiftStateCaption.Text←ShiftS←None buff←¯1↓∊((2 2 2⊤shiftState)/'Alt' 'Ctrl' 'Shift'),¨'+'
 n.Copy.Active←1

⍝ ⎕←(10↑(⍕key),' ',(⍕shiftState)),'= ',({''≡⍵:'' ⋄ ⍵,'+'}buff),None input   ⍝ Not anymore: stand-alone EXE now
⍝Done
