 {R}←Run debugFlag;verticalPosn;HP;VS;∆;allChildrenButLast;sz;maxLength;posn;buff;corr
⍝ Shows a GUI were the user can press any key (even) <escape> in order to see
⍝ what's carried by the associated "KeyPress" event.
⍝ See `Version` and `History` for details.
 ⎕IO←1 ⋄ ⎕ML←1 ⋄ ⎕WX←3
 {}{2704⌶1}⍣debugFlag⊣⍬  ⍝ Allow saving a CONTINUE WS
 {}{'⎕SE'⎕WS'Size'⍵}⍣(~debugFlag)⊣(230 800)
 R←⍬
 n←⎕NS'' ⍝ Used to collect references and global(s)
 ∆←''
 ∆,←('Caption' 'KeyPress Event')('Coord' 'Pixel')('MinButton' 0)('MaxButton' 0)('Size'(247 419))
 ∆,←⊂'Sizeable' 0               ⍝ Set this to 1 only when the Form needs adjustments
 n.TheForm←⎕NEW'Form'∆
 n.∆LastKey←''                  ⍝ Needed to recognize whether <escape> or <Alt+F4> was pressed twice in succession
 n.APLfont←⎕NEW'Font'(('PName' 'APL385 Unicode')('Size' 22))
 n.∆fontSize←2 ⎕NQ n.APLfont'GetTextSize' 'W'  ⍝ was "C"
 verticalPosn←⌊n.∆fontSize[1]×¯1.5+2.5×⍳8
⍝ ----- Create column 1: left-hand labels -----------------------------
 HP←n.∆fontSize[2]              ⍝ Horizontal position
 VS←n.∆fontSize[1]×2            ⍝ Vertical size
 ∆←('Size'(VS ⍬))('Attach'(4⍴'Top' 'Left'))
 'L1'n.TheForm.⎕WC'Label' ' [1] Object:'('Posn'(1⊃verticalPosn)HP),∆
 'L2'n.TheForm.⎕WC'Label' ' [2] Event:'('Posn'(2⊃verticalPosn)HP),∆
 'L3'n.TheForm.⎕WC'Label' ' [3] Input:'('Posn'(3⊃verticalPosn)HP),∆
 'L4'n.TheForm.⎕WC'Label' ' [4] Char Code:'('Posn'(4⊃verticalPosn)HP),∆
 'L5'n.TheForm.⎕WC'Label' ' [5] Key Number:'('Posn'(5⊃verticalPosn)HP),∆
 'L6'n.TheForm.⎕WC'Label' ' [6] Shift State:'('Posn'(6⊃verticalPosn)HP),∆
 ∆←'To exit click the close box or press either <Escape> or <Alt+F4> twice'
 'LC'n.TheForm.⎕WC'Label'∆('Posn'(10+7⊃verticalPosn)(n.∆fontSize[2]+5))('Size'VS ⍬)('FCol' 0 0 255)('Attach'(4⍴'Top' 'Left'))
 n.TheForm.Size←(10+⊃+⌿↑n.TheForm.LC.(Posn Size))800
 allChildrenButLast←¯1↓'Label'⎕WN n.TheForm
 sz←2⊃¨¨allChildrenButLast ⎕WG¨⊂'Posn' 'Size'
 allChildrenButLast ⎕WS¨⊂'Size'⍬(⌈/2⊃¨sz)
 HP←⌈/+/¨sz                             ⍝ Largest Posn+Size = Horiz positions (column 2)
⍝ ----- Create column 2: APL "edit" boxes -----------------------------
 maxLength←n.∆fontSize[2]×1+≢¨'KeyPress' 'Ctrl+Shift+Alt'
 ∆←('Size'(VS,1⊃maxLength))('Font'n.APLfont)('ReadOnly' 1)('Attach'(4⍴'Top' 'Left'))
 n.Object←n.TheForm.⎕NEW'Edit'((⊂'Posn'((1⊃verticalPosn)HP)),∆)
 n.Event←n.TheForm.⎕NEW'Edit'((⊂'Posn'((2⊃verticalPosn)HP)),∆)
 n.InputCode←n.TheForm.⎕NEW'Edit'((⊂'Posn'((3⊃verticalPosn)HP)),∆)
 n.CharCode←n.TheForm.⎕NEW'Edit'((⊂'Posn'((4⊃verticalPosn)HP)),∆)
 n.KeyNumber←n.TheForm.⎕NEW'Edit'((⊂'Posn'((5⊃verticalPosn)HP)),∆)
 n.ShiftState←n.TheForm.⎕NEW'Edit'((⊂'Posn'((6⊃verticalPosn)HP)),∆)
⍝ ----- Create more stuff ---------------------------------------------
 posn←(4⊃verticalPosn),HP+(1⊃maxLength)+n.∆fontSize[2]×0.5
 ∆←('Size'(VS ⍬))('Attach'(4⍴'Top' 'Left'))
 'D1'n.TheForm.⎕WC'Label' '(decimal)'('Posn'posn),∆
 buff←'D1'n.TheForm.⎕WG'Size'
 posn[2]+←buff[2]
 'D3'n.TheForm.⎕WC'Label'('Posn'posn),('Caption' ' ≡')('Font' 'APL')('Size'VS(n.∆fontSize[2]×2.5))('Attach'(4⍴'Top' 'Left'))
 buff←'D3'n.TheForm.⎕WG'Size'
 posn[2]+←buff[2]
 n.Decimal←n.TheForm.⎕NEW'Edit'(('Posn'posn)('Font'n.APLfont)('ReadOnly' 1)('Size'(VS,100))('Attach'(4⍴'Top' 'Left')))
 posn[2]←5+2⊃+⌿↑n.Decimal.(Posn Size)
 'D2'n.TheForm.⎕WC'Label' '(hex)'('Posn'posn),∆
⍝ ----- Create shift state buttons ------------------------------------
 posn←(6⊃verticalPosn),HP+(1⊃maxLength)
 'B0'n.TheForm.⎕WC'Label'('Posn'posn),('Caption' ' ≡')('Font' 'APL')('Size'VS(n.∆fontSize[2]×2.5))('Attach'(4⍴'Top' 'Left'))
 buff←'B0'n.TheForm.⎕WG'Size'
 posn[2]+←buff[2]
 n.ShiftStateCaption←n.TheForm.⎕NEW'Edit'(('Posn'posn)('Size'(VS,2⊃maxLength))('Font'n.APLfont)('ReadOnly' 1)('Attach'(4⍴'Top' 'Left')))
 buff←{(⎕WN ⍵)~'Font'⎕WN ⍵}n.TheForm
 buff←2⊃¨¨buff ⎕WG¨⊂'Posn' 'Size'
 buff←n.∆fontSize[2]+⌈/+/¨buff                  ⍝ Largest Posn+Size, plus right-hand pad
 corr←⌊{(1⊃⍵)×0.001×¯100+4⊃⍵}'.'⎕WG'DevCaps'    ⍝ Honour scaling
 n.TheForm.⎕WS('Size'⍬ buff)                    ⍝ Fix overall width
 n.TheForm.onKeyPress←'OnKeyPress'
 ⎕←'KeyPress info:'                             ⍝ Force the session to make an appearance
 ⎕NQ n.TheForm'GotFocus'                        ⍝ Force focus back to GUI
 ⎕DQ'.'
 {}{⎕OFF}⍣(~debugFlag)⊣⍬
⍝Done
